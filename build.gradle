buildscript {
    ext {
        logbackVersion = '1.2.3'
        jupiterVersion = '5.6.2'
        minIOVersion = '8.2.2'
        springBootStartVersion = '2.4.3'
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'club.javafamily'
version '0.0.1'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation "io.minio:minio:${minIOVersion}"
    compileOnly "org.springframework.boot:spring-boot-starter-web:${springBootStartVersion}"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${springBootStartVersion}"
    // test
    testImplementation "org.junit.jupiter:junit-jupiter:${jupiterVersion}"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = "${version}"
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'oss minio'
                description = 'JavaFamily OSS MinIO library'
                url = 'https://github.com/JavaFamilyClub/oss-minio'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'JavaFamilyClub'
                        name = 'JavaFamily'
                        email = 'javafamily.club@outlook.com'
                        organization = 'JavaFamily'
                    }
                }
                scm {
                    url = 'https://github.com/JavaFamilyClub/oss-minio'
                    connection = 'scm:git:git://github.com/JavaFamilyClub/oss-minio.git'
                    developerConnection = 'scm:git:ssh://git@github.com/JavaFamilyClub/oss-minio.git'
                }
            }
        }
    }
    repositories {
        maven {
            def snapshotsUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
            def releasesUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'

            url = project.version.endsWith('-SNAPSHOT') ? snapshotsUrl : releasesUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    options.addStringOption("charset", "UTF-8")
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

wrapper {
    gradleVersion '6.2.1'
}
